package pt.ist.fenixedu.vigilancies.domain;

enum pt.ist.fenixedu.vigilancies.domain.AttendingStatus as AttendingStatus;

class ExamCoordinator  {
    Boolean allowedToCreateGroups;
}

class UnavailablePeriod  {
    DateTime beginDate;
    DateTime endDate;
    String justification;
}

class Vigilancy  {
    Boolean active;
    AttendingStatus status;
}

class OtherCourseVigilancy extends Vigilancy {
    Boolean confirmed;
}

class OwnCourseVigilancy extends Vigilancy {
}

class VigilantGroup  {
    DateTime beginOfFirstPeriodForUnavailablePeriods;
    DateTime beginOfSecondPeriodForUnavailablePeriods;
    String contactEmail;
    String emailSubjectPrefix;  
    String convokeStrategy;
    DateTime endOfFirstPeriodForUnavailablePeriods;
    DateTime endOfSecondPeriodForUnavailablePeriods;
    String name;
    Integer pointsForConvoked;
    Integer pointsForDisconvoked;
    Integer pointsForDismissed;
    Integer pointsForDismissedTeacher;
    Integer pointsForMissing;
    Integer pointsForMissingTeacher;
    Integer pointsForTeacher;
    String rulesLink;
}

class VigilantWrapper  {
    BigDecimal pointsWeight;
    BigDecimal startPoints;
    Boolean convokable;
    String justification;
}

class accessControl.PersistentVigilancyGroup extends .org.fenixedu.bennu.core.domain.groups.PersistentGroup;

relation ConvokeForWrittenEvaluation {
    Vigilancy playsRole vigilancies {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.WrittenEvaluation playsRole writtenEvaluation;
}

relation VigilantAttendedWrittenEvaluation {
    Vigilancy playsRole attendedVigilancies {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.WrittenEvaluation playsRole attendedWrittenEvaluation;
}

relation ConvokingVigilantWrapper {
    VigilantWrapper playsRole vigilantWrapper;
    Vigilancy playsRole vigilancies {
        multiplicity *;
    }
}

relation VigilantGroupVigilantWrapper {
    VigilantGroup playsRole vigilantGroup;
    VigilantWrapper playsRole vigilantWrappers {
        multiplicity *;
    }
}

relation VigilantWrapperPerson {
    VigilantWrapper playsRole vigilantWrappers {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.Person playsRole person;
}

relation PersonIsUnavailable {
    .org.fenixedu.academic.domain.Person playsRole person;
    UnavailablePeriod playsRole unavailablePeriods {
        multiplicity *;
    }
}

relation VigilantWrapperRootDomainObject {
    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
    VigilantWrapper playsRole vigilantWrappers {
        multiplicity *;
    }
}

relation PersonIncompatibleVigilant {
    .org.fenixedu.academic.domain.Person playsRole incompatiblePerson;
    .org.fenixedu.academic.domain.Person playsRole incompatibleVigilant;
}

relation ExamCoordinatorBelongToUnit {
    ExamCoordinator playsRole examCoordinators {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole unit;
}

relation ExamCoordinatorInExecutionYear {
    ExamCoordinator playsRole examCoordinators {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.ExecutionYear playsRole executionYear;
}

relation ExamCoordinatorManagesVigilantGroup {
    ExamCoordinator playsRole examCoordinators {
        multiplicity *;
    }
    VigilantGroup playsRole vigilantGroups {
        multiplicity *;
    }
}

relation ExamCoordinatorPerson {
    ExamCoordinator playsRole examCoordinators {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.Person playsRole person;
}

relation ExecutionCourseBelongsToGroup {
    VigilantGroup playsRole vigilantGroup;
    .org.fenixedu.academic.domain.ExecutionCourse playsRole executionCourses {
        multiplicity *;
    }
}

relation RootDomainExamCoordinator {
    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
    ExamCoordinator playsRole examCoordinators {
        multiplicity *;
    }
}

relation RootDomainUnavailablePeriod {
    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
    UnavailablePeriod playsRole unavailablePeriods {
        multiplicity *;
    }
}

relation RootDomainVigilancy {
    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
    Vigilancy playsRole vigilancies {
        multiplicity *;
    }
}

relation RootDomainVigilantGroup {
    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
    VigilantGroup playsRole vigilantGroups {
        multiplicity *;
    }
}

relation VigilantGroupBelongsToUnit {
    VigilantGroup playsRole vigilantGroups {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole unit;
}

relation VigilantGroupInExecutionYear {
    VigilantGroup playsRole vigilantGroups {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.ExecutionYear playsRole executionYear;
}

relation PersistentVigilancyGroupVigilancy {
    public accessControl.PersistentVigilancyGroup playsRole vigilancyGroup {
        multiplicity 0..1;
    }
    public Vigilancy playsRole vigilancy {
        multiplicity 1..1;
    }
}

